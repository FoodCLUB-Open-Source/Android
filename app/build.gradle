plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.room'
    id 'com.google.relay' version '0.3.02'
    id 'com.google.devtools.ksp'
    id 'dagger.hilt.android.plugin'
    id("com.google.gms.google-services")
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'android.kotlin.foodclub'
    compileSdk 34

    defaultConfig {
        applicationId "android.kotlin.foodclub"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        development {
            minifyEnabled false
            shrinkResources false
            debuggable true
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

room {
    schemaDirectory "$projectDir/schemas"
}

dependencies {

    // AndroidX Core and Lifecycle Dependencies
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Kotlin and Coroutines
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.2"

    // Jetpack Compose and UI Related
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3:1.1.2'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation "androidx.navigation:navigation-compose:2.7.6"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.compose.material:material:1.5.4"
    debugImplementation 'androidx.compose.ui:ui-tooling:1.6.3'

    //Paging
    implementation "androidx.paging:paging-runtime-ktx:3.2.1"
    implementation "androidx.paging:paging-compose:3.3.0-alpha03"

    // Room Database
    implementation("androidx.room:room-runtime:2.6.1")
    annotationProcessor("androidx.room:room-compiler:2.6.1")
    ksp("androidx.room:room-compiler:2.6.1")
    implementation("androidx.room:room-ktx:2.6.1")
    implementation("androidx.room:room-paging:2.6.1")

    // Firebase
    implementation(platform("com.google.firebase:firebase-bom:32.7.2"))
    implementation("com.google.firebase:firebase-messaging")

    // Media, Video, and Camera Dependencies
    implementation "androidx.media3:media3-exoplayer:1.1.0"
    implementation "androidx.media3:media3-exoplayer-dash:1.1.0"
    implementation "androidx.media3:media3-ui:1.1.0"
    implementation "androidx.media3:media3-transformer:1.1.0"
    implementation "androidx.media3:media3-effect:1.1.0"
    implementation "androidx.media3:media3-common:1.1.0"
    implementation "androidx.camera:camera-lifecycle:1.3.2"
    implementation "androidx.camera:camera-video:1.3.2"
    implementation "androidx.camera:camera-view:1.3.2"
    implementation "androidx.camera:camera-extensions:1.3.2"

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.50'
    ksp 'com.google.dagger:hilt-compiler:2.50'
    implementation("androidx.hilt:hilt-navigation-compose:1.1.0")
    ksp 'androidx.hilt:hilt-compiler:1.1.0'

    // DataStore SharedPreferences
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation("androidx.datastore:datastore:1.0.0")

    // Coil
    implementation("io.coil-kt:coil-compose:2.4.0")

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Accompanist
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.31.2-alpha'
    implementation "com.google.accompanist:accompanist-permissions:0.23.1"
    implementation "com.google.accompanist:accompanist-pager:0.28.0"

    // Other
    implementation "com.airbnb.android:lottie-compose:4.2.0"
    implementation 'com.auth0.android:jwtdecode:2.0.2'
    implementation 'com.auth0:java-jwt:4.4.0'

    // FFmpeg
    implementation ("com.arthenica:ffmpeg-kit-full:6.0-2")

    // Splash Screen
    implementation "androidx.core:core-splashscreen:1.0.1"

    // Custom Tabs
    implementation "androidx.browser:browser:1.8.0"
}